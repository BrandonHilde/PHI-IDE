phi.Hello:OS16BITVideo
{
	str hello: 'Hello, World!\r\n';
    str newline: '\r\n';
	
	log hello newline;
	log 'Press any key to continue...';
	
	call OS16BIT.WaitForKeyPress;
	
	call OS16BITVideo.EnableVideoMode;
	call OS16BIT.JumpToSectorTwo;
}

phi.Drawing:OS16BITSectorTwo
{

	int ScreenW: 320;
	int ScreenH: 200;
	
	int lefty: 50;
	int Bot: 90;
	int botR: 0;
	int botL: 0;
	int direction: 0;
	
	int ballspeed: 1;
	int ballspeedy: 1;
	
	int maxspeed: 8;
	int paddleSpeed: 2;
	
	int ballx: 160;
	int bally: 100;
	
	int ballyDir: 0;
	
	int leftColx: 50;
	int rightColx: 50;
	
	int directr: 1;
	
	int righty: 50;

	int key:0;
	bln keyW:false;
	bln keyS:false;
	bln keyO:false;
	bln keyL:false;
	
	int leftPoints: 0;
	int rightPoints: 0;

	call OS16BIT.SetupInteruptTimer;
	call OS16BIT.SetupKeyboardInterupt;

	[OS16BIT.TimerEvent]

		call OS16BITVideo.DrawRectangle:
			0	0
			320	200	
			Colors.Black
		;
		
		
		if ballspeed > maxspeed
			ballspeed is maxspeed;
		;;


		
		#leftcontrol
		
		if lefty > 0
			if keyW is 1
				lefty--paddleSpeed;
			;;
		;;
		else
			lefty is 0;
		;;

			botL is lefty;
			botL++Bot;

		if key is 's'

			if botL < ScreenH
			
				lefty++paddleSpeed;
			;;
		;;
		
		#right		
		if righty > 0
			if key is 'o'
				righty--paddleSpeed;
			;;
		;;
		else
			righty is 0;
		;;

			botR is righty;
			botR++Bot;

		if key is 'l'

			if botR < ScreenH
			
				righty++paddleSpeed;
			;;
		;;
		
		#ball stuff
		
		if directr is 0
			ballx -- ballspeed;
			
			if ballx < 20
				if	bally > lefty
					leftColx is lefty;
					leftColx++Bot;
					
					if	bally < leftColx
						directr is 1;
						ballspeed++; #collision
					;;
				;;
			;;
		;;
		else	
			ballx ++ ballspeed;
			
			if ballx > 290
				if	bally > righty
					rightColx is righty;
					rightColx++Bot;
					
					if	bally < rightColx
						directr is 0;
						ballspeed++; #collision
					;;
				;;
			;;
		;;
		
		if ballyDir is 0
			bally--ballspeedy;
		;;
		else
			bally++ballspeedy;
		;;
		
		if bally < 11
			#bounce
			ballyDir is 1;
		;;
		
		if bally > 189
			#bounce
			ballyDir is 0;
		;;
		
		#score
		
		if ballx < 11
			ballx is 160;
			bally is 100;
			directr is 1;
			rightPoints++; #points scored
			ballspeed is 1;
		;;
		
		if ballx > 309
			ballx is 160;
			bally is 100;
			directr is 0;
			leftPoints++; #points scored
			ballspeed is 1;
		;;

		#draw rectangles
		
		if bally < 0
		
			call OS16BITVideo.DrawRectangle:
				ballx	bally
				10	10	
				Colors.Red
			;
		;;
		else
				call OS16BITVideo.DrawRectangle:
				ballx	bally
				10	10	
				Colors.Blue
			;
		;;
		
		call OS16BITVideo.DrawRectangle:
			10	lefty
			10	Bot	
			Colors.LightGreen
		;
		
		call OS16BITVideo.DrawRectangle:
			300	righty
			10	Bot
			Colors.LightGreen
		;

	[end]
	
	
	
	[OS16BIT.KeyboardEvent]

		call key is OS16BIT.GetKey;
		call keyW is OS16BIT.IsKeyDown:'w';

	[end]
	
}